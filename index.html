<script type="coffee">

# TODO
# - make more fun IDs (noun concat)
# - make mobile pretty
# - question lists
# - dark/light themes
# - askhole.io + telegram links
# - some copy explaining the thing
# - "invite friends" with way to copy URL to clipboard
# - keyboard shortcuts


#--- COMMON ---#

questions = [
    "What is 'the moon'?",
    "What's your favorite animal?",
    "Have you named your genitals?",
    "What is your alter ego?",
    "How do you predict you will die?",
    "What don't you know about yourself?",
    "When was the last time you did something for the first time?",
    "Who is your arch-nemesis?",
    "What are you the patron saint of?"
    ]

# Generate a random integer up to (not including) max
getRandomInt = (max) ->
    Math.floor(Math.random() * Math.floor(max))

# Get a random integer guaranteed to be different from a current one
getDifferentRandomInt = (max, cur) ->
    n = getRandomInt(max)
    while n == cur
        n = getRandomInt(max)
    n

# from https://stackoverflow.com/questions/1349404/generate-random-string-characters-in-javascript
# dec2hex :: Integer -> String
# i.e. 0-255 -> '00'-'ff'
dec2hex = (dec) ->
    ('0' + dec.toString(16)).substr(-2)

# from https://stackoverflow.com/questions/1349404/generate-random-string-characters-in-javascript
# generateId :: Integer -> String
generateId = (len) ->
    arr = new Uint8Array((len || 40) / 2)
    window.crypto.getRandomValues(arr)
    Array.from(arr, dec2hex).join('')



#--- STATE ---#

# State variables.
hash = fetch('hash')
party = {}

# Set the party hash.
setHash = (str) ->
    hash.val = str
    save(hash)

    if hash.val.length
        party = fetch('/' + hash.val)
    else
        party = {}



LOADPAGE = ->

    # Are we in a party?
    str = window.location.hash.substring(1)
    setHash(str)

    dom.BODY = ->
        hash = fetch('hash')
        DIV {},
            if hash.val
                PARTY_PAGE()
            else
                LANDING_PAGE()

    return true



PARTY_PAGE = ->

    DIV {className: 'bgwhirl'},
        HEADER text: 'PLAY WITH FRIENDS'
        QUESTION()



LANDING_PAGE = ->

    # TODO: Why is this necessary? It was done above. Scope issue?
    hash = fetch('hash')

    DIV {className: 'home_welcome'},
        HEADER text: 'PLAY WITH FRIENDS'
        DIV {className: 'landing_hero'}
        DIV {className: 'home_form'},
            onClick: =>
                id = generateId(10)
                window.location.href = document.baseURI + '#' + id
                setHash(id)
            BUTTON
                className: 'home_button'
                'create a party →'


dom.HEADER = (text) ->
    DIV {className: 'header'},
        SPAN(text)

dom.QUESTION = ->
    hash = fetch('hash')
    party = fetch('/' + hash.val)
    n = party.question_number

    if !n
        party.question_number = getRandomInt(questions.length)
        save(party)

    question = questions[n] || ''
    number = (n+1) || ''
    cardtext = '[loading…]'
    if question && number
        cardtext = '#' + number + ': ' + question

    DIV {className: 'party_wrap'},
        DIV {
                className: 'party_card',
                onClick: =>
                    party.question_number =
                        getDifferentRandomInt(questions.length, n)
                    save(party)
            },
            DIV {className: 'party_cardtext'},
                cardtext



# Reload the page whenever the URL hash changes.
window.onhashchange = -> LOADPAGE()

# Kickoff.
LOADPAGE()

</script>
<script src="https://stateb.us/client6.js" server='state://invisible.college'></script>
<link rel='stylesheet' href='https://cdnjs.cloudflare.com/ajax/libs/skeleton/2.0.4/skeleton.min.css'>
<link rel='stylesheet' href='common.css'>
<link href="https://fonts.googleapis.com/css2?family=Lato:ital@0;1&display=swap" rel="stylesheet">
<link href="https://fonts.googleapis.com/css2?family=Permanent+Marker&display=swap" rel="stylesheet">
